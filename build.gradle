/*
 * Forgelin build file.
 */

//================================================
// Pre-execute

buildscript {
    ext {
        // ==[ Variables ]========================
        mc_ver = '1.8'
        forge_ver = '11.14.1.1402'
        kotlin_ver = '0.12.200'
        mod_version = '0.1.0'
        // =======================================
    }
    repositories {
        mavenCentral()
        maven {
            name = 'Forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        // Sonatype (for SpecialSource in 1.7+)
        maven {
            name = "Sonatype Nexus"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_ver}"
    }
}

repositories {
    maven {
        name 'Forge'
        url 'http://files.minecraftforge.net/maven'
    }
    mavenCentral()
    maven {
        name 'MinecraftS3'
        url 'http://s3.amazonaws.com/Minecraft.Download/libraries'
    }
}

apply plugin: 'kotlin'
apply plugin: 'forge'

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade "org.jetbrains.kotlin:kotlin-stdlib:${kotlin_ver}"
    shade "org.jetbrains.kotlin:kotlin-reflect:${kotlin_ver}"
    testCompile "junit:junit:4.+"
}

// Setup Forge plugin
minecraft {
    version = mc_ver + "-" + forge_ver
    runDir = 'run'
    replace '@VERSION@', mod_version
    replace '@KOTLIN@', kotlin_ver

    // Note: Despite shading Kotlin, we _don't_ repackage it - this lets other mods use it, and saves us downloading libraries at runtime (ewh)
}

//================================================
// Jar data

// Grab system env
def env = System.getenv()

group = "io.drakon"
archivesBaseName = "forgelin"
version = "${mc_ver}-${mod_version}-${kotlin_ver}"

// Drone manifest
def droneManifest = manifest {
    if (env.DRONE != null) {
        attributes("Drone-Build": "true", "Drone-Build-ID": env.DRONE_BUILD_NUMBER, "Drone-Commit": env.DRONE_COMMIT,
                   "Drone-Branch": env.DRONE_BRANCH, "Drone-Repo-Slug": env.DRONE_REPO_SLUG)
        version += "-d${env.DRONE_BUILD_NUMBER}"
    } else {
        attributes("Drone-Build": "false")
    }
}

// Version manifest
def verManifest = manifest {
    attributes("Mod-Version": mod_version, "Forge-Version": mc_ver + "-" + forge_ver, "Kotlin-Version": kotlin_ver)
}

// FML Core manifest
def fmlManifest = manifest {
	attributes("FMLCorePlugin": "io.drakon.forgelin.CorePlugin", "FMLCorePluginContainsFMLMod": "true")
}

processResources {
    // Make sure this gets re-executed on updates.
    inputs.property "version", version
    inputs.property "MCversion", mc_ver
    inputs.property "kotlinver", kotlin_ver
    
    // Process mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        expand 'version':mod_version,'MCversion':mc_ver,'kotlinver':kotlin_ver
    }

    // Copy anything else directly
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
    }
}

//================================================
// Jar tasks

jar {
    // Shading
    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }

    // Merge Jenkins and Git manifests to form final manifest in final release jar
    manifest {
        from droneManifest, verManifest, fmlManifest
    }
}


//================================================
// Maven deployment
apply plugin: 'maven'

if (!project.hasProperty("DEPLOY_DIR")) {
    ext.DEPLOY_DIR = null
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://${DEPLOY_DIR}")
        }
    }
}

uploadArchives.onlyIf { return DEPLOY_DIR != null }
